plugins {
    id 'java'
    id 'jacoco'
}

jacoco {
    toolVersion = '0.8.7'
}

group 'camp.nextstep.edu'
version '1.0-SNAPSHOT'

jacocoTestReport {
    reports {
        html.enabled(true)
        xml.enabled(true)
        csv.enabled(false)
    }
    dependsOn test

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it,
                    excludes: [])
        }))
    }

    finalizedBy 'jacocoTestCoverageVerification'
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            enabled = false
            element = 'CLASS' // 클래스 단위로 체크

            // 라인 커버리지 제한을 70 % 설정.
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.7
            }
        }

        rule {
            enabled = true
            element = 'METHOD'

            // 빈 줄을 제외한 코드의 라인수를 최대 10 라인으로 제한합니다.
            limit {
                counter = 'LINE'
                value = 'TOTALCOUNT'
                maximum = 10
            }
        }
    }
}


repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation 'com.github.woowacourse-projects:mission-utils:1.0.0'
    implementation('com.github.E1psycongr00:code-checker:1.0.0')
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

test {
    useJUnitPlatform()
}